prototype(Neos.RedirectHandler.Ui:Module.ImportCsv) < prototype(Neos.Fusion:Component) {
    protocol = ${[]}

    indexAction = Neos.Fusion:UriBuilder {
        action = 'index'
    }

    renderer = afx`
        <div class="neos-content neos-container-fluid">
            <div class="neos-row-fluid" @if.protocolNotEmpty={props.protocol}>
                <h2>{I18n.translate('Neos.RedirectHandler.Ui:Modules:header.importProtocol')}</h2>
                <br/>
                <ul class="redirects-protocol-list">
                    <Neos.Fusion:Loop items={props.protocol} itemName="entry">
                        <li class={'redirects-protocol-entry--' + entry.type}>
                            <strong>{entry.type}:</strong> {entry.message}
                            <span @if.hasRedirect={entry.redirect}>
                                {entry.redirect.sourceUriPath} &rarr; {entry.redirect.targetUriPath} ({entry.redirect.statusCode})
                            </span>
                        </li>
                    </Neos.Fusion:Loop>
                </ul>
            </div>

            <div class="neos-footer">
                <a class="neos-button" href={props.indexAction}>
                    {I18n.translate('Neos.RedirectHandler.Ui:Modules:backToList')}
                </a>
            </div>
        </div>
    `
}
