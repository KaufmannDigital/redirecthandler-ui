prototype(Neos.RedirectHandler.Ui:Component.Redirect.List.Item) < prototype(Neos.Fusion:Component) {
    showHitCount = false
    redirect = null
    csrfToken = null
    className = ''
    mainClassName = 'redirect-table__row'

    identifier = ${Neos.RedirectHandler.Ui.Redirect.createIdentifier(this.redirect)}
    redirectData = ${Neos.RedirectHandler.Ui.Redirect.serialize(this.redirect)}
    isActive = ${Neos.RedirectHandler.Ui.Redirect.isActive(this.redirect)}

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        classNames = Neos.Fusion:Join {
            main = ${props.className}
            row = ${props.mainClassName}
            active = Neos.Fusion:Case {
                inActive {
                    condition = ${!props.isActive}
                    renderer = ${props.mainClassName + '--inactive'}
                }
                modified {
                    condition = ${Date.format(props.redirect.creationDateTime, 'U') != Date.format(props.redirect.lastModificationDateTime, 'U')}
                    renderer = ${props.mainClassName + '--modified'}
                }
                generated {
                    condition = ${props.redirect.type != 'manual'}
                    renderer = ${props.mainClassName + '--generated'}
                }
                default {
                    condition = ${true}
                    renderer = ${props.mainClassName + '--new'}
                }
            }
            @glue = ' '
        }

        deleteAction = Neos.Fusion:UriBuilder {
            action = 'delete'
        }
        updateAction = Neos.Fusion:UriBuilder {
            action = 'update'
        }

        renderer = afx`
            <tr class={props.classNames} data-redirect-id={props.identifier} data-redirect-props={props.redirectData}>
                <td class={props.mainClassName + '__column-status-code'}
                    title={I18n.translate('Neos.RedirectHandler.Ui:Modules:statusCodes.' + props.redirect.statusCode + '.tooltip')}>
                    {props.redirect.statusCode}
                </td>
                <td>{props.redirect.host || '&ndash;'}</td>
                <td title={props.redirect.sourceUriPath}>{Neos.RedirectHandler.Ui.Redirect.shortenPath(props.redirect.sourceUriPath)}</td>
                <td title={props.redirect.targetUriPath}>{Neos.RedirectHandler.Ui.Redirect.shortenPath(props.redirect.targetUriPath)}</td>
                <td class={props.mainClassName + '__column-start'}>
                    {props.redirect.startDateTime ? Date.format(props.redirect.startDateTime, 'd.m.Y H:i') : '&ndash;'}
                </td>
                <td class={props.mainClassName + '__column-end'}>
                    {props.redirect.endDateTime ? Date.format(props.redirect.endDateTime, 'd.m.Y H:i') : '&ndash;'}
                </td>
                <td>{props.redirect.comment || '&ndash;'}</td>
                <td class={props.mainClassName + '__column-hit-count'} @if.enabled={props.showHitCount}>
                    {props.redirect.hitCounter}
                    <span @if.hit={props.redirect.lastHit}>(Last hit at {Date.format(props.redirect.lastHit, 'd.m.Y H:i')})</span>
                </td>
                <td class={props.mainClassName + '__column-creation-date-time'} title={Date.format(props.redirect.creationDateTime, 'd.m.Y H:i:s')}>
                    {Date.format(props.redirect.creationDateTime, 'd.m.Y')}
                </td>
                <td>
                    {props.redirect.creator} <span @if.generated={props.redirect.type != 'manual'} class="redirect__type">({props.redirect.type})</span>
                </td>
                <td class="neos-action">
                    <button type="button" class="neos-button" title={I18n.translate('Neos.RedirectHandler.Ui:Modules:edit')}
                            data-edit-redirect-id={props.identifier}>
                        <i class="fa fa-pencil-alt icon-pencil icon-white"></i>
                    </button>
                    <form action={props.deleteAction} type="POST">
                        <input type="hidden" name="__csrfToken" value={String.htmlSpecialChars(props.csrfToken)}/>
                        <input type="hidden" name="moduleArguments[sourceUriPath]" value={props.redirect.sourceUriPath}/>
                        <input type="hidden" name="moduleArguments[host]" value={props.redirect.host}/>
                        <button type="submit" class="neos-button neos-button-danger" title={I18n.translate('Neos.RedirectHandler.Ui:Modules:delete')}
                                onclick={'return confirm("' + I18n.translate('Neos.RedirectHandler.Ui:Modules:confirmDelete') + '")'}>
                            <i class="fa fa-trash-alt icon-trash icon-white"></i>
                        </button>
                    </form>
                </td>
            </tr>
        `
    }
}
