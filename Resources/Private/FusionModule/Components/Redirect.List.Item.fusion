prototype(Neos.RedirectHandler.Ui:Component.Redirect.List.Item) < prototype(Neos.Fusion:Component) {
    showHitCount = false
    redirect = null
    csrfToken = null
    deleteAction = Neos.Fusion:UriBuilder {
        action = 'delete'
    }
    updateAction = Neos.Fusion:UriBuilder {
        action = 'update'
    }
    identifier = ${Neos.RedirectHandler.Ui.Redirect.createIdentifier(this.redirect)}
    redirectData = ${Neos.RedirectHandler.Ui.Redirect.serialize(this.redirect)}
    className = ''
    className.@process.addBaseClass = ${value + ' redirect-table__row'}

    renderer = afx`
        <tr class={props.className} data-redirect-id={props.identifier} data-redirect-props={props.redirectData}>
            <td class="redirect-table__row__column-status-code">{props.redirect.statusCode}</td>
            <td>{props.redirect.host || '&ndash;'}</td>
            <td>{Neos.RedirectHandler.Ui.Redirect.shortenPath(props.redirect.sourceUriPath)}</td>
            <td>{Neos.RedirectHandler.Ui.Redirect.shortenPath(props.redirect.targetUriPath)}</td>
            <td>{props.redirect.startDateTime ? Date.format(props.redirect.startDateTime, 'd.m.Y H:i:s') : '&ndash;'}</td>
            <td>{props.redirect.endDateTime ? Date.format(props.redirect.endDateTime, 'd.m.Y H:i:s') : '&ndash;'}</td>
            <td>{props.redirect.comment || '&ndash;'}</td>
            <td class="redirect-table__row__column-hit-count" @if.enabled={props.showHitCount}>
                {props.redirect.hitCounter}
                <span @if.hit={props.redirect.lastHit}>(Last hit at {Date.format(props.redirect.lastHit, 'd.m.Y H:i:s')})</span>
            </td>
            <td class="redirect-table__row__column-creation-date-time" title={Date.format(props.redirect.creationDateTime, 'd.m.Y H:i:s')}>
                {Date.format(props.redirect.creationDateTime, 'd.m.Y')}
            </td>
            <td>
                {props.redirect.creator} <span @if.generated={props.redirect.type != "manual"}>({props.redirect.type})</span>
            </td>
            <td class="neos-action">
                <button type="button" class="neos-button" title={I18n.translate('Neos.RedirectHandler.Ui:Modules:edit')}
                        data-edit-redirect-id={props.identifier}>
                    <i class="fa fa-pencil-alt icon-pencil icon-white"></i>
                </button>
                <form action={props.deleteAction} type="POST">
                    <input type="hidden" name="__csrfToken" value={String.htmlSpecialChars(props.csrfToken)}/>
                    <input type="hidden" name="moduleArguments[sourceUriPath]" value={props.redirect.sourceUriPath}/>
                    <input type="hidden" name="moduleArguments[host]" value={props.redirect.host}/>
                    <button type="submit" class="neos-button neos-button-danger" title={I18n.translate('Neos.RedirectHandler.Ui:Modules:delete')}
                            onclick={'return confirm("' + I18n.translate('Neos.RedirectHandler.Ui:Modules:confirmDelete') + '")'}>
                        <i class="fa fa-trash-alt icon-trash icon-white"></i>
                    </button>
                </form>
            </td>
        </tr>
    `
}
